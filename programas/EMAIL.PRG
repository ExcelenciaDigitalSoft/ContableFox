*******************************************************************
*- EMAIL.PRG - EMAIL CODE FILE
*- Controls Communication with MS Exchange
*- Calls application object
*******************************************************************
*- Author	: Matthew J. McDonnell, Jr.
*- Copywrite: 1997, all rights reserved
*******************************************************************
* - SYNOPSIS
* - The procedures and methods here are used to email and fax
*******************************************************************
* 
#INCLUDE "INCLUDE\STRINGS.H"

*-- One procedure to do it all
PROCEDURE SendInternetMail
  PARAMETERS tcAddress, tcSubject, tcText
  
  LOCAL loSess
  
  loSess = EmailSession()
  =SendMail(loSess, tcAddress, tcSubject, tcText)
  loSess.logoff  && If this procedure is not used (i.e. for multiple emails)
  *- the logoff method must be called by the application that created the email session.
  

*-- Creates new email session, finds mailbox for current user  
PROCEDURE EmailSession
  LOCAL loSess
  
  SET MESS TO "Logging Into Email Server....."
  loSess = CREATEOBJECT("MAPI.Session")
  loSess.logon()   && - optional parameter is a profile, see above for using logoff

RETURN loSess

*-- Based on passed active email session, builds and sends email
PROCEDURE SendMail
  *- Format for tcAddress:
  *- FAX - "FAX:name@555-1234"
  *- email - "joe@hisdomain.com"
  PARAMETERS toSess, tcAddress, tcSubject, tcText
    
  LOCAL loMsg, loAddr, lcEsc
  
  SET MESS TO "Building Email....."
  
  loMsg = toSess.outbox.messages.add  && returns message/outbox object
  
  loMsg.subject = tcSubject
  loMsg.text = tcText
  *-  I haven't tested it yet, but I believe that the following code can be put in a 
  *-  DO WHILE or FOR ENDFOR with the same object reference each pass in order to send
  *-  email to mulitiple recipients
  loAddr = loMsg.recipients.add  && returns address object
  loAddr.AddressEntry.Type = IIF("FAX"$tcAddress, "FAX", "EMAIL")
  IF "FAX"$tcAddress
    loAddr.Name = SUBSTR(tcAddress, 5, LEN(tcAddress)-AT("@",tcAddress)-5)
    loAddr.Address = tcAddress
    loAddr.AddressEntry.Address = SUBSTR(tcAddress, 5)
  ELSE
    loAddr.Name = ALLT(tcAddress)
  ENDIF
 
  loMsg.update  && - update the outbox

  SET MESS TO "Sending Message...."
  loMsg.send(1,0,0)
  
  SET MESS TO "Message Sent."

*- build a message
PROC EmailBody
	#DEFINE HEADERLINE REPL('*', lnWidth)

	LOCAL lcReturn, lnWidth, lnOldWidth, lnI

	lnWidth = 70

	lnOldWidth = SET('MEMOWIDTH')

	SET MEMOWIDTH TO lnWidth

	lcReturn = HEADERLINE + CRLF + ;
	           PADR(" Message From Application", lnWidth ) + CRLF + ;
	           PADR(" Automatically Generated by application ",lnWidth ) + CRLF + ;
	           HEADERLINE + CRLF	 

	* - This would be where you would extract info from a table.
	lcReturn = lcReturn + "This is the message" + CRLF + ;
	           HEADERLINE + CRLF + HEADERLINE

	SET MEMOWIDTH TO lnOldWidth

RETURN lcReturn